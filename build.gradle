buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.6'
        classpath 'com.uber:okbuck:0.7.0'
    }
}
apply plugin: "com.uber.okbuck"

allprojects {
    project -> project.apply from: rootProject.file('dependencies.gradle') 
    repositories { jcenter() }
    // to avoid duplicate class errors
    configurations.all { 
        exclude group: "com.amazonaws" 
        exclude module: "javax.annotation"
        exclude module: "support-annotations"
        exclude module: "annotations"
        resolutionStrategy.force deps.support.cardView
    }
}

subprojects { project ->
    group = "com.studio"
    afterEvaluate {
        project.configurations.all {
            exclude group: 'org.apache.httpcomponents', module: 'httpclient'
            exclude group: 'org.json', module: 'json'
            exclude group: 'commons-logging', module: 'commons-logging'
            exclude group: 'org.javassist', module: 'javassist'
            exclude module: 'xmlParserAPIs'
            exclude module: 'xpp3'
            exclude module: 'xmlpull'
        }

        if (project.plugins.hasPlugin('java')) {
            addCommonConfigurationForJavaModules(project)
        } else if (project.plugins.hasPlugin('com.android.application')
                || project.plugins.hasPlugin('com.android.library')) {
            addCommonConfigurationForAndroidModules(project)
        }

        if (project.plugins.hasPlugin('com.android.application')) {
            project.android{
                signingConfigs {
                    debug {
                        storeFile file('config/signing/debug.keystore')
                    }
                }
                buildTypes {
                    release {
                        signingConfig signingConfigs.debug
                    }
                }
                packagingOptions {
                    exclude 'META-INF/LICENSE'
                }
                dexOptions {
                    javaMaxHeapSize "4g"
                }
            }
        }
    }
}

def addCommonConfigurationForJavaModules(Project project) {
    project.sourceCompatibility = JavaVersion.VERSION_1_7
    project.targetCompatibility = JavaVersion.VERSION_1_7
}

def addCommonConfigurationForAndroidModules(Project project) {
    
    project.android {
        compileSdkVersion deps.build.compileSdkVersion
        buildToolsVersion deps.build.buildToolsVersion

        defaultConfig {
            minSdkVersion deps.build.minSdkVersion
            targetSdkVersion deps.build.targetSdkVersion
            vectorDrawables.useSupportLibrary = true
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        lintOptions {
            abortOnError false
            // To avoid linting which requires gigantic amounts of heap
            //checkReleaseBuilds false
        }
        
        packagingOptions {
            pickFirst  'META-INF/license.txt'
            pickFirst  'META-INF/LICENSE'
        }
    }
}
